plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id 'net.researchgate.release' version '2.6.0'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group = "de.wellnerbou"

// user and key configuration for bintray deployment:
// ./gradlew bintrayUpload -PpublishUser=<USER> -PpublishKey=<KEY>
if (!project.hasProperty("publishUser")) {
    ext.publishUser = ""
}
if (!project.hasProperty("publishKey")) {
    ext.publishKey = ""
}
if (!project.hasProperty("publishPassword")) {
    ext.publishPassword = ""
}
if (!project.hasProperty("bintrayOrganisation")) {
    ext.bintrayOrganisation = 'wellnerbou-polopoly'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

afterReleaseBuild.dependsOn bintrayUpload

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        credentials {
            username polopolyRepoUser
            password polopolyRepoPassword
        }
        url "http://maven.polopoly.com/nexus/content/groups/polopoly-public/"
    }
}

if (!project.hasProperty("repo")) {
    ext.repo = "$buildDir/repo"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'polopoly-policy-mock'
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

publishing {
    repositories {
        maven {
            credentials {
                username "$publishUser"
                password "$publishPassword"
            }
            // change to point to your repo, e.g. http://my.org/repo
            url "$repo"
        }
    }
}

bintray {
    user = "$publishUser"
    key = "$publishKey"
    publications = ['mavenJava']

    dryRun = false
    publish = true

    pkg {
        userOrg = "$bintrayOrganisation"
        repo = 'maven'
        name = 'polopoly-policy-mock'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/paulwellnerbou/polopoly-policy-mock'
    }
}

dependencies {
    def polopolyVersion = '10.16.5-fp1'

    compileOnly("com.polopoly:polopoly:${polopolyVersion}:api")
    compileOnly("com.polopoly:polopoly:${polopolyVersion}")
    compileOnly("com.polopoly:legacy-site-plugin:${polopolyVersion}")
    compileOnly("org.mockito:mockito-all:1.10.17")

    testCompile 'org.assertj:assertj-core:3.10.0'
    testImplementation("com.polopoly:polopoly:${polopolyVersion}:api")
    testImplementation("com.polopoly:polopoly:${polopolyVersion}")
    testImplementation("com.polopoly:legacy-site-plugin:${polopolyVersion}")
    testImplementation("org.mockito:mockito-all:1.10.17")
}
